#
# qmake configuration for macx-ios-clang
#

!load(device_config): error("Could not successfully load device configuration.")

isEmpty(QMAKE_MAC_SDK): QMAKE_MAC_SDK = iphoneos

# iOS is considered a variant of MacOS by Apple. We follow this to
# keep things simple, e.g. by defining Q_OS_MAC _and_ Q_OS_IOS.
include(../../common/mac.conf)

include(../../common/gcc-base-ios.conf)
include(../../common/xcode.conf)
include(../../common/clang.conf)
include(../../common/clang-mac.conf)

# Extract Xcode version using xcodebuild
version_info = $$system("$${QMAKE_XCODE_DEVELOPER_PATH}/usr/bin/xcodebuild -version")
QMAKE_IOS_XCODE_VERSION = $$member(version_info, 1)
unset(version_info)

# Version check
lessThan(QMAKE_IOS_XCODE_VERSION, "4.3"): error("This mkspec requires Xcode 4.3 or later")

include(../../common/ios.conf)
include(../../common/ios/clang.conf)
include(../../common/ios/qmake.conf)

load(qt_config)
